
# Lex rule:

# Import has to come before Ident because Ident matches "import"
Lex = Import / Ident / EnumConst / Num / LParen / RParen / LBracket / RBracket / Eq / Semi / Dot / ArithOp / W;

Import = "import";
Ident = ident:([a-zA-Z_][a-zA-Z0-9_]*);
EnumConst = enumConst:('#' Ident);
Num = num:(Digit+ / Digit* Dot Digit+ [fF]?);
LParen = '('; 
RParen = ')'; 
Eq = '=';
Semi = ';';
Dot = '.';
LBracket = '[';
RBracket = ')';
ArithOp = '+' / '-' / '*' / '/';
Digit = [0-9];

# Don't match zero or more whitespace chars in Lex rule, so that we can see where lex fails 
W = (' ' / '\t' / '\n' / '\r' / '\f');
WS = W*;

# Post-lex grammar:

Program = ^ WS (ImportStatement WS)* (Assignment WS)+;

ImportStatement = Import WS import:Package WS Semi;

Package = Ident (Dot Package)?;

Assignment = assign:(lvalue:Ident WS Eq WS Expr WS Semi);

Accessor = accessor:(Ident WS (('[' WS lookup:Expr WS ']') WS / ('.' WS dot:Accessor WS))+ / Ident);

Expr[5] = '(' WS Expr WS ')';

# Accesor must come before Ident since Ident is a prefix of an Accessor

Expr[4] = Accessor / EnumConst / Num;

Expr[3] = arith:((op:'-' WS)+ Expr);

Expr[2] = arith:(Expr WS (op:('*' / '/') WS Expr WS)+);

Expr[1] = arith:(Expr WS (op:('+' / '-') WS Expr WS)+);

